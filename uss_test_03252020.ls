/PROG  USS_TEST_03252020
/ATTR
OWNER		= MNEDITOR;
COMMENT		= "";
PROG_SIZE	= 2396;
CREATE		= DATE 20-03-25  TIME 23:17:10;
MODIFIED	= DATE 20-04-07  TIME 22:39:18;
FILE_NAME	= ;
VERSION		= 0;
LINE_COUNT	= 126;
MEMORY_SIZE	= 2820;
PROTECT		= READ_WRITE;
TCD:  STACK_SIZE	= 0,
      TASK_PRIORITY	= 50,
      TIME_SLICE	= 0,
      BUSY_LAMP_OFF	= 0,
      ABORT_REQUEST	= 0,
      PAUSE_REQUEST	= 0;
DEFAULT_GROUP	= 1,*,*,*,*;
CONTROL_CODE	= 00000000 00000000;
/MN
   1:  CALL ZERO    ;
   2:  R[6]=0    ;
   3:  R[7]=0    ;
   4:   ;
   5:  LBL[21] ;
   6:   ;
   7:  !ACTIVE USS ITERATOR ;
   8:  IF (R[6]<>3) THEN ;
   9:  R[6]=R[6]+1    ;
  10:  ELSE ;
  11:  R[6]=1    ;
  12:  ENDIF ;
  13:  !TURN OFF INACTIVE USS PWR ;
  14:  IF (R[6]=1) THEN ;
  15:  !kill uss 2 & 3 pwr ;
  16:  DO[33]=ON ;
  17:  DO[35]=OFF ;
  18:  DO[37]=OFF ;
  19:  ELSE ;
  20:  IF (R[6]=2) THEN ;
  21:  !kill uss 1 & 3 pwr ;
  22:  DO[33]=OFF ;
  23:  DO[35]=ON ;
  24:  DO[37]=OFF ;
  25:  ELSE ;
  26:  IF (R[6]=3) THEN ;
  27:  !kill uss 1 & 2 pwr ;
  28:  DO[33]=OFF ;
  29:  DO[35]=OFF ;
  30:  DO[37]=ON ;
  31:  ENDIF ;
  32:  ENDIF ;
  33:  ENDIF ;
  34:   ;
  35:   ;
  36:  !the following lines are hardcode ;
  37:  R[6]=3    ;
  38:  DO[37]=ON ;
  39:   ;
  40:   ;
  41:  !TRAVEL TO APPROACH ;
  42:L PR[3] 3000mm/sec FINE    ;
  43:   ;
  44:  !set trigger distance (in mm) ;
  45:  !(USS trig's before at this dist) ;
  46:  R[8]=1000    ;
  47:  PR[4,3]=((-1)*R[8]) ;
  48:  !conv MM to active USS pulses ;
  49:  IF (R[6]=1) THEN ;
  50:  R[3]=R[8]*R[9]    ;
  51:  ELSE ;
  52:  IF (R[6]=2) THEN ;
  53:  R[3]=R[8]*R[10]    ;
  54:  ELSE ;
  55:  IF (R[6]=3) THEN ;
  56:  //R[3]=R[8]*R[11]    ;
  57:  R[3]=(65536-(R[8]*R[11])) ;
  58:  ENDIF ;
  59:  ENDIF ;
  60:  ENDIF ;
  61:  !set lower bound of trig window ;
  62:  R[12]=R[3]*.99    ;
  63:  MONITOR ... ;
  64:   ;
  65:   ;
  66:  !FAST DESCEND ;
  67:  !SETS USS NO TRIG FAULT DIST ;
  68:  SKIP CONDITION GI[R[6]]<=R[3] AND GI[R[6]]>R[12]    ;
  69:L PR[3] 250mm/sec FINE Offset,PR[4] Skip,LBL[20]    ;
  70:  PR[6]=LPOS    ;
  71:   ;
  72:  !DO IF NO USS TRIG ;
  73:  !MOVE TO FAIL POS ;
  74:L PR[7] 3000mm/sec FINE    ;
  75:  R[4]=9999.99    ;
  76:  JMP LBL[30] ;
  77:   ;
  78:  LBL[20] ;
  79:  PR[6]=LPOS    ;
  80:  !RETRACT FOR SLOW DESCEND ;
  81:L PR[6] 500mm/sec FINE Offset,PR[5]    ;
  82:   ;
  83:  !SLOW DESCEND ;
  84:L PR[3] 10mm/sec FINE Offset,PR[4]    ;
  85:  R[1]=0    ;
  86:  WAIT    .50(sec) ;
  87:  PR[6]=LPOS    ;
  88:  R[1]=PR[4,3]    ;
  89:   ;
  90:  IF R[6]=1,JMP LBL[23] ;
  91:  IF R[6]=2,JMP LBL[24] ;
  92:  IF R[6]=3,JMP LBL[25] ;
  93:   ;
  94:  !SLOW DESC USS-1 ;
  95:  LBL[23] ;
  96:  R[2]=GI[1]    ;
  97:  CALL UIT502USSTOMM    ;
  98:  JMP LBL[30] ;
  99:   ;
 100:  !SLOW DESC USS-2 ;
 101:  LBL[24] ;
 102:  R[2]=GI[2]    ;
 103:  CALL UIT508USSTOMM    ;
 104:  JMP LBL[30] ;
 105:   ;
 106:  !SLOW DESC USS-3 ;
 107:  LBL[25] ;
 108:  R[7]=R[7]+1    ;
 109:  R[2]=GI[3]    ;
 110:  CALL UBUSSTOMM    ;
 111:  JMP LBL[30] ;
 112:   ;
 113:   ;
 114:  LBL[30] ;
 115:  CALL LOGREG(R[5],SR[1],R[6],R[4],R[7]) ;
 116:   ;
 117:  IF (R[7]>=5000) THEN ;
 118:  JMP LBL[99] ;
 119:  ENDIF ;
 120:   ;
 121:  JMP LBL[21] ;
 122:   ;
 123:  !END PROGRAM ;
 124:  LBL[99] ;
 125:  CALL ZERO    ;
 126:  END ;
/POS
/END
